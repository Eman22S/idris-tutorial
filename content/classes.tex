\section{Type Classes}
\label{sec:classes}

We often want to define functions which work across several different data types.
For example, we would like arithmetic operators to work on \texttt{Int}, \texttt{Integer} and \texttt{Float} at the very least.
We would like \texttt{==} to work on the majority of data types.
We would like to be able to display different types in a uniform way.

To achieve this, we use a feature which has proved to be effective in Haskell, namely \emph{type classes}.
To define a type class, we provide a collection of overloaded operations which describe the interface for \emph{instances} of that class.
A simple example is the \texttt{Show} type class, which is defined in the prelude and provides an interface for converting values to \texttt{String}:

\begin{code}
class Show a where
    show : a -> String
\end{code}

\noindent
This generates a function of the following type (which we call a \emph{method} of the  \texttt{Show} class):

\begin{code}
show : Show a => a -> String
\end{code}

\noindent
We can read this as: ``under the constraint that \texttt{a} is an instance of \texttt{Show}, take an input \texttt{a} and return a \texttt{String}.''
An instance of a class is defined with an \texttt{instance} declaration, which provides implementations of the function for a specific type.
For example, the \texttt{Show} instance for \texttt{Nat} could be defined as:

\begin{code}
instance Show Nat where
    show Z = "Z"
    show (S k) = "s" ++ show k
\end{code}

\begin{lstlisting}[style=stdout]
Idris> show (S (S (S Z)))
"sssZ" : String
\end{lstlisting}

\noindent
Only one instance of a class can be given for a type --- instances may not overlap.
To help with resolution, the arguments of an instance must be constructors
(either data or type constructors), variables or constants (i.e. you cannot
give an instance for a function).
Instance declarations can themselves have constraints.
For example, to define a \texttt{Show} instance for vectors, we need to know that there is a \texttt{Show}  instance for the element type, because we are going to use it to convert each element to a \texttt{String}:

\begin{code}
instance Show a => Show (Vect n a) where
    show xs = "[" ++ show' xs ++ "]" where
        show' : Vect n a -> String
        show' Nil        = ""
        show' (x :: Nil) = show x
        show' (x :: xs)  = show x ++ ", " ++ show' xs
\end{code}

%\noindent
%\textbf{Remark: } The type of the auxiliary function \texttt{show'} is
%important. The type variables \texttt{a} and \texttt{n} which are part of the
%instance declaration for \texttt{Show (Vect a n)} are fixed across the entire
%instance declaration. As a result, we do not have to constrain \texttt{a}
%again. Furthermore, it means that if we use \texttt{n} in the type, it refers
%to the (fixed) length of the outermost list \texttt{xs}. Therefore,
%we use a different name for the length \texttt{n'} in \texttt{show'}.

\subsection{Default Definitions}

The library defines an \texttt{Eq} class which provides an interface for comparing values for equality or inequality, with instances for all of the built-in types:

\begin{code}
class Eq a where
    (==) : a -> a -> Bool
    (/=) : a -> a -> Bool
\end{code}

\noindent
To declare an instance of a type, we have to give definitions of all of the methods.
For example, for an instance of \texttt{Eq} for \texttt{Nat}:

\begin{code}
instance Eq Nat where
    Z     == Z     = True
    (S x) == (S y) = x == y
    Z     == (S y) = False
    (S x) == Z     = False

    x /= y = not (x == y)
\end{code}

\noindent
It is hard to imagine many cases where the \texttt{/=} method will be anything other than the negation of the result of applying the \texttt{==} method.
It is therefore convenient to give a default definition for each method in the class declaration, in terms of the other method:

\begin{code}
class Eq a where
    (==) : a -> a -> Bool
    (/=) : a -> a -> Bool

    x /= y = not (x == y)
    x == y = not (x /= y)
\end{code}

\noindent
A minimal complete definition of an \texttt{Eq} instance requires either \texttt{==} or \texttt{/=} to be defined, but does not require both.
If a method definition is missing, and there is a default definition for it, then the default is used instead.

\subsection{Extending Classes}

Classes can also be extended.
A logical next step from an equality relation \texttt{Eq} is to define an ordering relation \texttt{Ord}.
We can define an \texttt{Ord} class which inherits methods from \texttt{Eq} as well as defining some of its own:

\begin{code}
data Ordering = LT | EQ | GT
\end{code} 

\begin{code}
class Eq a => Ord a where
    compare : a -> a -> Ordering

    (<) : a -> a -> Bool
    (>) : a -> a -> Bool
    (<=) : a -> a -> Bool
    (>=) : a -> a -> Bool
    max : a -> a -> a
    min : a -> a -> a
\end{code}

\noindent
The \texttt{Ord} class allows us to compare two values and determine their ordering. 
Only the \texttt{compare} method is required; every other method has a default definition.
Using this we can write functions such as \texttt{sort}, a function which sorts a list into increasing order, provided that the element type of the list is in the \texttt{Ord} class.
We give the constraints on the type variables left of the fat arrow \texttt{=>}, and the function type to the right of the fat arrow:

\begin{code}
sort : Ord a => List a -> List a
\end{code}

\noindent
Functions, classes and instances can have multiple constraints.
Multiple constaints are written in brackets in a comma separated list, for example:

\begin{code}
sortAndShow : (Ord a, Show a) => List a -> String
sortAndShow xs = show (sort xs)
\end{code}

\subsection{Functors and Applicatives}

\label{sec:functor}

So far, we have seen single parameter type classes, where the parameter is of
type \texttt{Type}.  In general, there can be any number (greater than 0) of
parameters, and the parameters can have \emph{any} type.  If the type of the
parameter is not \texttt{Type}, we need to give an explicit type declaration.
For example, the \texttt{Functor} class is defined in the library:

\begin{code}
class Functor (f : Type -> Type) where
    map : (m : a -> b) -> f a -> f b
\end{code}

\noindent
A functor allows a function to be applied across a structure, for example
to apply a function to every element in a \texttt{List}:

\begin{code}
instance Functor List where
  map f []      = []
  map f (x::xs) = f x :: map f xs
\end{code}

\begin{lstlisting}[style=stdout]
Idris> map (*2) [1..10]
[2, 4, 6, 8, 10, 12, 14, 16, 18, 20] : List Integer
\end{lstlisting}

\noindent
Having defined \texttt{Functor}, we can define \texttt{Applicative}
which abstracts the notion of function application:

\begin{code}
infixl 2 <*> 

class Functor f => Applicative (f : Type -> Type) where 
    pure  : a -> f a
    (<*>) : f (a -> b) -> f a -> f b 
\end{code} 

\subsection{Monads and \texttt{do}-notation}

\label{sec:monad}

The \texttt{Monad} class allows us to encapsulate binding and computation, and
is the basis of \texttt{do}-notation introduced in Section~\ref{sect:do}.
It extends \texttt{Applicative} as defined above, and is defined as
follows:

\begin{code}
class Applicative m => Monad (m : Type -> Type) where
    (>>=)  : m a -> (a -> m b) -> m b
\end{code}

\noindent
Inside a \texttt{do} block, the following syntactic transformations are
applied:

\begin{itemize}
\item \texttt{x <- v; e} becomes \texttt{v >>= ($\backslash$x => e)}
\item \texttt{v; e} becomes \texttt{v >>= ($\backslash$\_ => e)}
\item \texttt{let x = v; e} becomes \texttt{let x = v in e}
\end{itemize}

\noindent
\texttt{IO} is an instance of \texttt{Monad}, defined using primitive functions.
We can also define an instance for \texttt{Maybe}, as follows:

\begin{code}
instance Monad Maybe where
    Nothing  >>= k = Nothing
    (Just x) >>= k = k x
\end{code}

\noindent
Using this we can, for example, define a function which adds two \texttt{Maybe Int}s, using the monad to encapsulate the error handling:

\begin{code}
m_add : Maybe Int -> Maybe Int -> Maybe Int
m_add x y = do x' <- x -- Extract value from x
               y' <- y -- Extract value from y
               return (x' + y') -- Add them 
\end{code}

\noindent
This function will extract the values from \texttt{x} and \texttt{y}, if they are available, or return \texttt{Nothing} if they are not.
Managing the \texttt{Nothing} cases is achieved by the \texttt{>>=} operator, hidden by the \texttt{do} notation.

\begin{lstlisting}[style=stdout]
*classes> m_add (Just 20) (Just 22) 
Just 42 : Maybe Int
*classes> m_add (Just 20) Nothing 
Nothing : Maybe Int
\end{lstlisting}

\subsubsection*{\texttt{!}-notation}

In many cases, using \texttt{do}-notation can make programs unnecessarily
verbose, particularly in cases such as \texttt{m\_add} above where the value
bound is used once, immediately. In these cases, we can use a shorthand
version, as follows:

\begin{code}
m_add : Maybe Int -> Maybe Int -> Maybe Int
m_add x y = return (!x + !y)
\end{code}

\noindent
The notation \texttt{!expr} means that the expression \texttt{expr} should
be evaluated and then implicitly bound. Conceptually,
we can think of \texttt{!} as being a prefix function with the following type:

\begin{code}
(!) : m a -> a
\end{code}

\noindent
Note, however, that it is not really a function, merely syntax! In practice,
a subexpression \texttt{!expr} will lift \texttt{expr} as high as possible within
its current scope, bind it to a fresh name \texttt{x}, and replace \texttt{!expr}
with \texttt{x}. Expressions are lifted depth first, left to right. In
practice, \texttt{!}-notation allows us to program in a more direct style,
while still giving a notational clue as to which expressions are monadic.

For example, the expression\ldots

\begin{code}
let y = 42 in f !(g !(print y) !x) 
\end{code}

\ldots is lifted to:

\begin{code}
let y = 42 in do y' <- print y
                 x' <- x
                 g' <- g y' x'
                 f g'
\end{code}

\subsubsection*{Monad comprehensions}

The list comprehension notation we saw in Section~\ref{sec:listcomp} is more
general, and applies to anything which is an instance of both \texttt{Monad}
and \texttt{Alternative}:

\begin{code}
class Applicative f => Alternative (f : Type -> Type) where
    empty : f a
    (<|>) : f a -> f a -> f a
\end{code}

\noindent
In general, a comprehension takes the form \texttt{[ exp | qual1, qual2,
\ldots, qualn ]} where \texttt{quali} can be one of:

\begin{itemize}
\item A generator \texttt{x <- e}
\item A \emph{guard}, which is an expression of type \texttt{Bool}
\item A let binding \texttt{let x = e}
\end{itemize}

\noindent
To translate a comprehension \texttt{[exp | qual1, qual2, \ldots, qualn]}, first any qualifier \texttt{qual} which is a \emph{guard} is translated to \texttt{guard qual}, using the following function:

\begin{code}
guard : Alternative f => Bool -> f ()
\end{code} 

\noindent
Then the comprehension is converted to \texttt{do} notation:

\begin{code}
do { qual1; qual2; ...; qualn; return exp; }
\end{code} 

\noindent
Using monad comprehensions, an alternative definition for \texttt{m\_add} would be:

\begin{code}
m_add : Maybe Int -> Maybe Int -> Maybe Int
m_add x y = [ x' + y' | x' <- x, y' <- y ]
\end{code} 

\subsection{Idiom brackets}

While \texttt{do} notation gives an alternative meaning to sequencing, idioms give an alternative meaning to \emph{application}.
The notation and larger example in this section is inspired by Conor McBride and Ross Paterson's paper ``Applicative
Programming with Effects''~\cite{idioms}.

First, let us revisit \texttt{m\_add} above. All it is really doing is applying an operator to two values extracted from \texttt{Maybe Int}'s.
We could abstract out the application:

\begin{code}
m_app : Maybe (a -> b) -> Maybe a -> Maybe b
m_app (Just f) (Just a) = Just (f a)
m_app _        _        = Nothing
\end{code} 

\noindent
Using this, we can write an alternative \texttt{m\_add} which uses this alternative notion of function application, with explicit calls to \texttt{m\_app}:

\begin{code}
m_add' : Maybe Int -> Maybe Int -> Maybe Int
m_add' x y = m_app (m_app (Just (+)) x) y
\end{code} 

\noindent
Rather than having to insert \texttt{m\_app} everywhere there is an
application, we can use \remph{idiom brackets} to do the job for us.  To do
this, we can make \texttt{Maybe} an instance of \texttt{Applicative} as
follows, where \texttt{<\*>} is defined in the same way as \texttt{m\_app}
above (this is defined in the \Idris{} library):

\begin{code}
instance Applicative Maybe where
    pure = Just

    (Just f) <*> (Just a) = Just (f a)
    _        <*> _        = Nothing
\end{code} 

\noindent
Using \remph{idiom brackets} we can use this instance as follows, where a
function application \texttt{[| f a1 \dots an |]} is translated into
\texttt{pure f <\*> a1 <\*> \dots <\*> an}:

\begin{code}
m_add' : Maybe Int -> Maybe Int -> Maybe Int
m_add' x y = [| x + y |]
\end{code} 

\subsubsection{An error-handling interpreter}

Idiom notation is commonly useful when defining evaluators.
McBride and Paterson describe such an evaluator~\cite{idioms}, for a language similar to the following:

\begin{code}
data Expr = Var String      -- variables
          | Val Int         -- values
          | Add Expr Expr   -- addition
\end{code} 

\noindent
Evaluation will take place relative to a context mapping variables (represented as \texttt{String}s) to integer values, and can possibly fail.
We define a data type \texttt{Eval} to wrap an evaluator:

\begin{code}
data Eval : Type -> Type where
     MkEval : (List (String, Int) -> Maybe a) -> Eval a
\end{code} 

\noindent
Wrapping the evaluator in a data type means we will be able to make it an instance of a type class later. We begin by defining a function to retrieve values from the context during evaluation:

\begin{code}
fetch : String -> Eval Int
fetch x = MkEval (\e => fetchVal e) where
    fetchVal : List (String, Int) -> Maybe Int
    fetchVal [] = Nothing
    fetchVal ((v, val) :: xs) = if (x == v)
                                  then (Just val)
                                  else (fetchVal xs)
\end{code} 
  
\noindent
When defining an evaluator for the language, we will be applying functions in the context of an \texttt{Eval}, so it is natural to make \texttt{Eval} an instance of \texttt{Applicative}.
Before \texttt{Eval} can be an instance of \texttt{Applicative} it is necessary to make \texttt{Eval} an instance of \texttt{Functor}:

\begin{code}
instance Functor Eval where
    map f (MkEval g) = MkEval (\e => map f (g e))

instance Applicative Eval where
    pure x = MkEval (\e => Just x)

    (<*>) (MkEval f) (MkEval g) = MkEval (\x => app (f x) (g x)) where
        app : Maybe (a -> b) -> Maybe a -> Maybe b
        app (Just fx) (Just gx) = Just (fx gx)
        app _         _         = Nothing
\end{code}

\noindent
Evaluating an expression can now make use of the idiomatic application to handle errors:

\begin{code}
eval : Expr -> Eval Int
eval (Var x)   = fetch x
eval (Val x)   = [| x |]
eval (Add x y) = [| eval x + eval y |]
  
runEval : List (String, Int) -> Expr -> Maybe Int
runEval env e = case eval e of
    MkEval envFn => envFn env
\end{code} 

\subsection{Named Instances}

It can be desirable to have multiple instances of a type class, for example to provide alternative methods for sorting or printing values.
To achieve this, instances can be \remph{named} as follows:

\begin{code}
instance [myord] Ord Nat where
   compare Z (S n)     = GT
   compare (S n) Z     = LT
   compare Z Z         = EQ
   compare (S x) (S y) = compare @{myord} x y
\end{code}

\noindent
This declares an instance as normal, but with an explicit name, \texttt{myord}.
The syntax \texttt{compare @\{myord\}} gives an explicit instance to \texttt{compare}, otherwise it would use the default instance for \texttt{Nat}.
We can use this, for example, to sort a list of \texttt{Nat}s in reverse.
Given the following list:

\begin{code}
testList : List Nat
testList = [3,4,1]
\end{code}

\noindent
\ldots we can sort it using the default \texttt{Ord} instance, then the named instance \texttt{myord} as follows, at the \Idris{} prompt:

\begin{lstlisting}[style=stdout]
*named_instance> show (sort testList)
"[sO, sssO, ssssO]" : String
*named_instance> show (sort @{myord} testList)
"[ssssO, sssO, sO]" : String
\end{lstlisting}

\subsection{Determining Parameters}

When a class has more than one parameter, it can help resolution if the
parameters used to resolve the type class are restricted. For example:

\begin{code}
class Monad m => MonadState s (m : Type -> Type) | m where
    get : m s
    put : s -> m ()
\end{code}

\noindent
In this class, only \texttt{m} needs to be known to resolve this class, and
\texttt{s} can then be determined from the instance. This is declared
with the \texttt{| m} after the class declaration. We call \texttt{m}
a \emph{determining parameter} of the \texttt{MonadState} class, because it
is the parameter used to resolve an instance. 

When checking for overlapping instances, only the determining parameters
are taken into account. For example, given a class:

\begin{code}
class Forget a b | a where
    forget : a -> b
\end{code}

\noindent
The following instances are considered overlapping, because they have
the same determining parameter:

\begin{code}
instance Forget Float Int
instance Forget Float Integer
\end{code}


