\section{Example: The Well-Typed Interpreter}
\label{sect:interp}

In this section, we'll use the features we've seen so far to write a larger example, an interpreter for a simple functional programming language, with variables, function application, binary operators and an \texttt{if...then...else} construct.
We will use the dependent type system to ensure that any programs which can be represented are well-typed.

\subsection{Representing Languages}
\label{sect:interp:representation}

First, let us define the types in the language.
We have integers, booleans, and functions, represented by \texttt{Ty}:

\begin{code}
data Ty = TyInt | TyBool | TyFun Ty Ty
\end{code}

\noindent
We can write a function to translate these representations to a concrete \Idris{} type --- remember that types are first class, so can be calculated just like any other value:

\begin{code}
interpTy : Ty -> Type
interpTy TyInt       = Int
interpTy TyBool      = Bool
interpTy (TyFun A T) = interpTy A -> interpTy T
\end{code}

\noindent
We're going to define a representation of our language in such a way that only well-typed programs can be represented.
We'll index the representations of expressions by their type and the types of local variables (the context), which we'll be using regularly as an implicit argument, so we define everything in a \texttt{using} block:

\begin{code}
using (G:Vect n Ty)
\end{code}

\noindent
The full representation of expressions is given in Listing~\ref{exprty}.
They are indexed by the types of the local variables, and the type of the expression itself:

\begin{code}
data Expr : Vect n Ty -> Ty -> Type
\end{code}

\noindent
Since expressions are indexed by their type, we can read the typing rules of the language from the definitions of the constructors.
Let us look at each constructor in turn.

\begin{code}[caption={Expression representation},label=exprty, float=htp]
data HasType : (i : Fin n) -> Vect n Ty -> Ty -> Type where
    Stop : HasType FZ (t :: G) t
    Pop  : HasType k G t -> HasType (FS k) (u :: G) t

data Expr : Vect n Ty -> Ty -> Type where
    Var : HasType i G t -> Expr G t
    Val : (x : Int) -> Expr G TyInt
    Lam : Expr (a :: G) t -> Expr G (TyFun a t)
    App : Expr G (TyFun a t) -> Expr G a -> Expr G t
    Op  : (interpTy a -> interpTy b -> interpTy c) ->
          Expr G a -> Expr G b -> Expr G c
    If  : Expr G TyBool ->
          Lazy (Expr G a) ->
          Lazy (Expr G a) -> Expr G a
\end{code}

\noindent
We use a nameless representation for variables --- they are \emph{de Bruijn indexed}.
Variables are represented by a proof of their membership in the context, \texttt{HasType i G T}, which is a proof that variable \texttt{i} in context  \texttt{G} has type \texttt{T}. This is defined as follows:

\begin{code}
data HasType : (i : Fin n) -> Vect n Ty -> Ty -> Type where
    Stop : HasType FZ (t :: G) t
    Pop  : HasType k G t -> HasType (FS k) (u :: G) t
\end{code}

\noindent
We can treat \emph{Stop} as a proof that the most recently defined variable is well-typed, and \emph{Pop n} as a proof that, if the \texttt{n}th most recently defined variable is well-typed, so is the \texttt{n+1}th.
In practice, this means we use \texttt{Stop} to refer to the most recently defined variable, \texttt{Pop Stop} to refer to the next, and so on, via the \texttt{Var} constructor:

\begin{code}
Var : HasType i G t -> Expr G t
\end{code}

\noindent
So, in an expression \verb!\x,\y. x y!, the variable \texttt{x} would have a de Bruijn index of 1, represented as \texttt{Pop Stop}, and \texttt{y 0}, represented as \texttt{Stop}.
We find these by counting the number of lambdas between the definition and the use.

\noindent
A value carries a concrete representation of an integer:

\begin{code}
Val : (x : Int) -> Expr G TyInt
\end{code}

\noindent
A lambda creates a function.
In the scope of a function of type \texttt{a -> t}, there is a new local variable of type \texttt{a}, which is expressed by the context index:

\begin{code}
Lam : Expr (a :: G) t -> Expr G (TyFun a t)
\end{code}

\noindent
Function application produces a value of type \texttt{t} given a function from  \texttt{a} to \texttt{t} and a value of type \texttt{a}:

\begin{code}
App : Expr G (TyFun a t) -> Expr G a -> Expr G t
\end{code}

\noindent
We allow arbitrary binary operators, where the type of the operator informs what the types of the arguments must be:

\begin{code}
Op : (interpTy a -> interpTy b -> interpTy c) ->
     Expr G a -> Expr G b -> Expr G c
\end{code}

\noindent
Finally, if expressions make a choice given a boolean.
Each branch must have the same type, and we will evaluate the branches
lazily so that only the branch which is taken need be evaluated:

\begin{code}
If : Expr G TyBool ->
     Lazy (Expr G a) ->
     Lazy (Expr G a) ->
     Expr G a
\end{code}

\subsection{Writing the Interpreter}
\label{sec:interp:eval}

When we evaluate an \texttt{Expr}, we'll need to know the values in scope, as well as their types.
\texttt{Env} is an environment, indexed over the types in scope.
Since an environment is just another form of list, albeit with a strongly specified connection to the vector of local variable types, we use the usual \texttt{::} and \texttt{Nil} constructors so that we can use the usual list syntax.
Given a proof that a variable is defined in the context, we can then produce a value from the environment:

\begin{code}
data Env : Vect n Ty -> Type where
    Nil  : Env Nil
    (::) : interpTy a -> Env G -> Env (a :: G)

lookup : HasType i G t -> Env G -> interpTy t
lookup Stop    (x :: xs) = x
lookup (Pop k) (x :: xs) = lookup k xs
\end{code}

\begin{code}[caption={Intepreter definition},label=interpdef]
interp : Env G -> Expr G t -> interpTy t
interp env (Var i)     = lookup i env
interp env (Val x)     = x
interp env (Lam sc)    = \x => interp (x :: env) sc
interp env (App f s)   = interp env f (interp env s)
interp env (Op op x y) = op (interp env x) (interp env y)
interp env (If x t e)  = if interp env x then interp env t
                                         else interp env e
\end{code}

\noindent
Given this, an interpreter (Listing~\ref{interpdef}) is a function which translates an \texttt{Expr} into a concrete \Idris{} value with respect to a specific environment:

\begin{code}
interp : Env G -> Expr G t -> interpTy t
\end{code}

\noindent
To translate a variable, we simply look it up in the environment:

\begin{code}
interp env (Var i) = lookup i env
\end{code}

\noindent
To translate a value, we just return the concrete representation of the value:

\begin{code}
interp env (Val x) = x
\end{code}

\noindent
Lambdas are more interesting.
In this case, we construct a function which interprets the scope of the lambda with a new value in the environment.
So, a function in the object language is translated to an \Idris{} function:

\begin{code}
interp env (Lam sc) = \x => interp (x :: env) sc
\end{code}

\noindent
For an application, we interpret the function and its argument and apply it directly.
We know that interpreting \texttt{f} must produce a function, because of its type:

\begin{code}
interp env (App f s) = interp env f (interp env s)
\end{code}

\noindent
Operators and interpreters are, again, direct translations into the equivalent \Idris{} constructs.
For operators, we apply the function to its operands directly, and for \texttt{If}, we apply the \Idris{} \texttt{if...then...else} construct directly.

\begin{code}
interp env (Op op x y) = op (interp env x) (interp env y)
interp env (If x t e)  = if interp env x then interp env t
                                         else interp env e
\end{code}

\subsection{Testing}
\label{sec:interp:testing}

We can make some simple test functions.
Firstly, adding two inputs \verb!\x. \y. y + x! is written as follows:

\begin{code}
add : Expr G (TyFun TyInt (TyFun TyInt TyInt))
add = Lam (Lam (Op (+) (Var Stop) (Var (Pop Stop))))
\end{code}

\noindent
More interestingly, a factorial function \texttt{fact} (e.g.\ \texttt{$\textbackslash$x. if (x == 0) then 1 else (fact (x-1) * x)}), can be written as:

\begin{code}
fact : Expr G (TyFun TyInt TyInt)
fact = Lam (If (Op (==) (Var Stop) (Val 0))
               (Val 1)
               (Op (*) (App fact (Op (-) (Var Stop) (Val 1)))
                       (Var Stop)))
\end{code}

\subsection{Running}
\label{sec:interp:running}

To finish, we write a \texttt{main} program which interprets the factorial function on user input:

\begin{code}
main : IO ()
main = do putStr "Enter a number: "
          x <- getLine
          print (interp [] fact (cast x))
\end{code}

\noindent
Here, \texttt{cast} is an overloaded function which converts a value from one type to another if possible.
Here, it converts a string to an integer, giving 0 if the input is invalid.
An example run of this program at the \Idris{} interactive environment is shown in Listing~\ref{factrun}.

\begin{lstlisting}[float=h,caption={Running the well-typed interpreter}, label=factrun, style=stdout]
$ idris interp.idr
     ____    __     _
    /  _/___/ /____(_)____
    / // __  / ___/ / ___/     Version ^\version{}^
  _/ // /_/ / /  / (__  )      http://www.idris-lang.org/
 /___/\__,_/_/  /_/____/       Type :? for help

Type checking ./interp.idr
*interp> :exec
Enter a number: 6
720
*interp>
\end{lstlisting}

\subsubsection*{Aside: \texttt{cast}}

The prelude defines a type class \texttt{Cast} which allows conversion between types:

\begin{code}
class Cast from to where
    cast : from -> to
\end{code}

\noindent
It is a \emph{multi-parameter} type class, defining the source type and object type of the cast.
It must be possible for the type checker to infer \emph{both} parameters at the point where the cast is applied.
There are casts defined between all of the primitive types, as far as they make sense.

%\subsection{Unit testing}

%Recall the \texttt{so} data type:

%\begin{code}
%data so : Bool -> Type where
%  oh : so True
%\end{code}

%\noindent
%This requires its parameter to be a \texttt{True} value.
%One simple application of this is to perform unit testing of functions at compile time.
%For example, we know that  \texttt{4! = 24} so we can set up a test case as follows:

%\begin{code}
%unitTestFac : so (interp [] fact 4 == 24)
%unitTestFac = oh
%\end{code}

%\noindent
%For the program to compile, the test case must pass. If we change \texttt{24} to \texttt{25}, say, we will get an error message like the following:

%\begin{lstlisting}
%Type checking ./interp.idr
%interp.idr:64:Can't unify so True with so False

%Specifically:
%	 Can't unify True with False
%\end{lstlisting}
